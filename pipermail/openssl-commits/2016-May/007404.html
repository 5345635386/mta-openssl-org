<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<HTML>
 <HEAD>
   <TITLE> [openssl-commits] [openssl]  master update
   </TITLE>
   <LINK REL="Index" HREF="https://mta.openssl.org/pipermail/openssl-commits/2016-May/index.html" >
   <LINK REL="made" HREF="mailto:openssl-commits%40openssl.org?Subject=Re%3A%20%5Bopenssl-commits%5D%20%5Bopenssl%5D%20%20master%20update&In-Reply-To=%3C1463690066.090238.31796.nullmailer%40dev.openssl.org%3E">
   <META NAME="robots" CONTENT="index,nofollow">
   <style type="text/css">
       pre {
           white-space: pre-wrap;       /* css-2.1, curent FF, Opera, Safari */
           }
   </style>
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="007402.html">
   <LINK REL="Next"  HREF="007405.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[openssl-commits] [openssl]  master update</H1>
    <B>Andy Polyakov</B> 
    <A HREF="mailto:openssl-commits%40openssl.org?Subject=Re%3A%20%5Bopenssl-commits%5D%20%5Bopenssl%5D%20%20master%20update&In-Reply-To=%3C1463690066.090238.31796.nullmailer%40dev.openssl.org%3E"
       TITLE="[openssl-commits] [openssl]  master update">appro at openssl.org
       </A><BR>
    <I>Thu May 19 20:34:26 UTC 2016</I>
    <P><UL>
        <LI>Previous message: <A HREF="007402.html">[openssl-commits] [openssl]  master update
</A></li>
        <LI>Next message: <A HREF="007405.html">[openssl-commits] [openssl]  master update
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#7404">[ date ]</a>
              <a href="thread.html#7404">[ thread ]</a>
              <a href="subject.html#7404">[ subject ]</a>
              <a href="author.html#7404">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>The branch master has been updated
       via  e33826f01bd78af76e0135c8dfab3387927a82bb (commit)
      from  2e6d7799ffc47604d06e0465afeb84b91aff8006 (commit)


- Log -----------------------------------------------------------------
commit e33826f01bd78af76e0135c8dfab3387927a82bb
Author: Andy Polyakov &lt;<A HREF="../../../mailman/listinfo/openssl-commits.html">appro at openssl.org</A>&gt;
Date:   Sun May 15 17:01:15 2016 +0200

    Add assembly CRYPTO_memcmp.
    
    GH: #102
    
    Reviewed-by: Richard Levitte &lt;<A HREF="../../../mailman/listinfo/openssl-commits.html">levitte at openssl.org</A>&gt;

-----------------------------------------------------------------------

Summary of changes:
 crypto/alphacpuid.pl    | 28 +++++++++++++++++++++++
 crypto/arm64cpuid.pl    | 20 +++++++++++++++++
 crypto/armv4cpuid.pl    | 30 +++++++++++++++++++++++++
 crypto/c64xpluscpuid.pl | 24 ++++++++++++++++++++
 crypto/cryptlib.c       |  2 ++
 crypto/ia64cpuid.S      | 60 ++++++++++++++++++++++++++++++++++++++++---------
 crypto/pariscid.pl      | 31 +++++++++++++++++++++++++
 crypto/ppccpuid.pl      | 26 +++++++++++++++++++++
 crypto/s390xcpuid.S     | 27 ++++++++++++++++++++++
 crypto/sparccpuid.S     | 34 ++++++++++++++++++++++++++++
 crypto/x86_64cpuid.pl   | 22 ++++++++++++++++++
 crypto/x86cpuid.pl      | 25 +++++++++++++++++++++
 12 files changed, 318 insertions(+), 11 deletions(-)

diff --git a/crypto/alphacpuid.pl b/crypto/alphacpuid.pl
index 6356b00..6c7fd4c 100644
--- a/crypto/alphacpuid.pl
+++ b/crypto/alphacpuid.pl
@@ -134,6 +134,34 @@ OPENSSL_cleanse:
 	bne	$17,.Little
 .Ldone: ret	($26)
 .end	OPENSSL_cleanse
+
+.globl	CRYPTO_memcmp
+.ent	CRYPTO_memcmp
+CRYPTO_memcmp:
+	.frame	$30,0,$26
+	.prologue 0
+	xor	$0,$0,$0
+	beq	$18,.Lno_data
+
+	xor	$1,$1,$1
+	nop
+.Loop_cmp:
+	ldq_u	$2,0($16)
+	subq	$18,1,$18
+	ldq_u	$3,0($17)
+	extbl	$2,$16,$2
+	lda	$16,1($16)
+	extbl	$3,$17,$3
+	lda	$17,1($17)
+	xor	$3,$2,$2
+	or	$2,$0,$0
+	bne	$18,.Loop_cmp
+
+	subq	$31,$0,$0
+	srl	$0,63,$0
+.Lno_data:
+	ret	($26)
+.end	CRYPTO_memcmp
 ___
 {
 my ($out,$cnt,$max)=(&quot;\$16&quot;,&quot;\$17&quot;,&quot;\$18&quot;);
diff --git a/crypto/arm64cpuid.pl b/crypto/arm64cpuid.pl
index c0af608..caa3387 100755
--- a/crypto/arm64cpuid.pl
+++ b/crypto/arm64cpuid.pl
@@ -100,6 +100,26 @@ OPENSSL_cleanse:
 	cbnz	x1,.Little	// len!=0?
 	ret
 .size	OPENSSL_cleanse,.-OPENSSL_cleanse
+
+.globl	CRYPTO_memcmp
+.type	CRYPTO_memcmp,%function
+.align	4
+CRYPTO_memcmp:
+	eor	w3,w3,w3
+	cbz	x2,.Lno_data	// len==0?
+.Loop_cmp:
+	ldrb	w4,[x0],#1
+	ldrb	w5,[x1],#1
+	eor	w4,w4,w5
+	orr	w3,w3,w4
+	subs	x2,x2,#1
+	b.ne	.Loop_cmp
+
+.Lno_data:
+	neg	w0,w3
+	lsr	w0,w0,#31
+	ret
+.size	CRYPTO_memcmp,.-CRYPTO_memcmp
 ___
 
 print $code;
diff --git a/crypto/armv4cpuid.pl b/crypto/armv4cpuid.pl
index 2c02f08..33c893d 100644
--- a/crypto/armv4cpuid.pl
+++ b/crypto/armv4cpuid.pl
@@ -105,6 +105,36 @@ OPENSSL_cleanse:
 #endif
 .size	OPENSSL_cleanse,.-OPENSSL_cleanse
 
+.global	CRYPTO_memcmp
+.type	CRYPTO_memcmp,%function
+.align	4
+CRYPTO_memcmp:
+	eor	ip,ip,ip
+	cmp	r2,#0
+	beq	.Lno_data
+	stmdb	sp!,{r4,r5}
+
+.Loop_cmp:
+	ldrb	r4,[r0],#1
+	ldrb	r5,[r1],#1
+	eor	r4,r4,r5
+	orr	ip,ip,r4
+	subs	r2,r2,#1
+	bne	.Loop_cmp
+
+	ldmia	sp!,{r4,r5}
+.Lno_data:
+	neg	r0,ip
+	mov	r0,r0,lsr#31
+#if __ARM_ARCH__&gt;=5
+	bx	lr
+#else
+	tst	lr,#1
+	moveq	pc,lr
+	.word	0xe12fff1e	@ bx	lr
+#endif
+.size	CRYPTO_memcmp,.-CRYPTO_memcmp
+
 #if __ARM_MAX_ARCH__&gt;=7
 .arch	armv7-a
 .fpu	neon
diff --git a/crypto/c64xpluscpuid.pl b/crypto/c64xpluscpuid.pl
index 3dcd691..9efe120 100644
--- a/crypto/c64xpluscpuid.pl
+++ b/crypto/c64xpluscpuid.pl
@@ -18,6 +18,7 @@ $code.=&lt;&lt;___;
 	.if	__TI_EABI__
 	.asg	OPENSSL_rdtsc,_OPENSSL_rdtsc
 	.asg	OPENSSL_cleanse,_OPENSSL_cleanse
+	.asg	CRYPTO_memcmp,_CRYPTO_memcmp
 	.asg	OPENSSL_atomic_add,_OPENSSL_atomic_add
 	.asg	OPENSSL_wipe_cpu,_OPENSSL_wipe_cpu
 	.asg	OPENSSL_instrument_bus,_OPENSSL_instrument_bus
@@ -87,6 +88,29 @@ _OPENSSL_cleanse:
    [A1]	STB	A2,*A4++[2]
 	.endasmfunc
 
+	.global	_CRYPTO_memcmp
+_CRYPTO_memcmp:
+	.asmfunc
+	MV	A6,B0
+  [!B0]	BNOP	RA
+||[!B0]	ZERO	A4
+   [B0]	MVC	B0,ILC
+|| [B0]	ZERO	A0
+	NOP	4
+
+	SPLOOP	1
+	LDBU	*A4++,A1
+||	LDBU	*B4++,B1
+	NOP	4
+	XOR.L	B1,A1,A2
+	SPKERNEL 1,0
+||	OR.S	A2,A0,A0
+
+	BNOP	RA,3
+	ZERO.L	A4
+  [A0]	MVK	1,A4
+	.endasmfunc
+
 	.global	_OPENSSL_atomic_add
 _OPENSSL_atomic_add:
 	.asmfunc
diff --git a/crypto/cryptlib.c b/crypto/cryptlib.c
index 92e323c..8e189ec 100644
--- a/crypto/cryptlib.c
+++ b/crypto/cryptlib.c
@@ -316,6 +316,7 @@ void OPENSSL_die(const char *message, const char *file, int line)
 #endif
 }
 
+#if !defined(OPENSSL_CPUID_OBJ)
 /* volatile unsigned char* pointers are there because
  * 1. Accessing a variable declared volatile via a pointer
  *    that lacks a volatile qualifier causes undefined behavior.
@@ -347,3 +348,4 @@ int CRYPTO_memcmp(const volatile void * volatile in_a,
 
     return x;
 }
+#endif
diff --git a/crypto/ia64cpuid.S b/crypto/ia64cpuid.S
index bf5abc3..f942648 100644
--- a/crypto/ia64cpuid.S
+++ b/crypto/ia64cpuid.S
@@ -2,6 +2,12 @@
 // On Win64i compile with ias.exe.
 .text
 
+#if defined(_HPUX_SOURCE) &amp;&amp; !defined(_LP64)
+#define	ADDP	addp4
+#else
+#define	ADDP	add
+#endif
+
 .global	OPENSSL_cpuid_setup#
 .proc	OPENSSL_cpuid_setup#
 OPENSSL_cpuid_setup:
@@ -131,9 +137,7 @@ OPENSSL_wipe_cpu:
 .proc	OPENSSL_cleanse#
 OPENSSL_cleanse:
 { .mib;	cmp.eq		p6,p0=0,r33	    // len==0
-#if defined(_HPUX_SOURCE) &amp;&amp; !defined(_LP64)
-	addp4		r32=0,r32
-#endif
+	ADDP		r32=0,r32
 (p6)	br.ret.spnt	b0		};;
 { .mib;	and		r2=7,r32
 	cmp.leu		p6,p0=15,r33	    // len&gt;=15
@@ -166,14 +170,51 @@ OPENSSL_cleanse:
 (p6)	br.ret.sptk.many	b0	};;
 .endp	OPENSSL_cleanse#
 
+.global	CRYPTO_memcmp#
+.proc	CRYPTO_memcmp#
+.align	32
+.skip	16
+CRYPTO_memcmp:
+	.prologue
+{ .mib;	mov		r8=0
+	cmp.eq		p6,p0=0,r34	    // len==0?
+(p6)	br.ret.spnt	b0		};;
+	.save		ar.pfs,r2
+{ .mib;	alloc		r2=ar.pfs,3,5,0,8
+	.save		ar.lc,r3
+	mov		r3=ar.lc
+	brp.loop.imp	.Loop_cmp_ctop,.Loop_cmp_cend-16
+					}
+{ .mib;	sub		r10=r34,r0,1
+	.save		pr,r9
+	mov		r9=pr		};;
+{ .mii;	ADDP		r16=0,r32
+	mov		ar.lc=r10
+	mov		ar.ec=4		}
+{ .mib;	ADDP		r17=0,r33
+	mov		pr.rot=1&lt;&lt;16	};;
+
+.Loop_cmp_ctop:
+{ .mib;	(p16)	ld1	r32=[r16],1
+	(p18)	xor	r34=r34,r38	}
+{ .mib;	(p16)	ld1	r36=[r17],1
+	(p19)	or	r8=r8,r35
+	br.ctop.sptk	.Loop_cmp_ctop	};;
+.Loop_cmp_cend:
+
+{ .mib;	cmp.ne		p6,p0=0,r8
+	mov		ar.lc=r3	};;
+{ .mib;
+(p6)	mov		r8=1
+	mov		pr=r9,0x1ffff
+	br.ret.sptk.many	b0	};;
+.endp	CRYPTO_memcmp#
+
 .global	OPENSSL_instrument_bus#
 .proc	OPENSSL_instrument_bus#
 OPENSSL_instrument_bus:
 { .mmi;	mov		r2=r33
-#if defined(_HPUX_SOURCE) &amp;&amp; !defined(_LP64)
-	addp4		r32=0,r32
-#endif
-					}
+	ADDP		r32=0,r32	}
 { .mmi;	mov		r8=ar.itc;;
 	mov		r10=r0
 	mov		r9=r8		};;
@@ -208,10 +249,7 @@ OPENSSL_instrument_bus:
 .proc	OPENSSL_instrument_bus2#
 OPENSSL_instrument_bus2:
 { .mmi;	mov		r2=r33			// put aside cnt
-#if defined(_HPUX_SOURCE) &amp;&amp; !defined(_LP64)
-	addp4		r32=0,r32
-#endif
-					}
+	ADDP		r32=0,r32	}
 { .mmi;	mov		r8=ar.itc;;
 	mov		r10=r0
 	mov		r9=r8		};;
diff --git a/crypto/pariscid.pl b/crypto/pariscid.pl
index 03895d5..f82e27a 100644
--- a/crypto/pariscid.pl
+++ b/crypto/pariscid.pl
@@ -138,6 +138,37 @@ L\$done
 ___
 }
 {
+my ($in1,$in2,$len)=(&quot;%r26&quot;,&quot;%r25&quot;,&quot;%r24&quot;);
+
+$code.=&lt;&lt;___;
+	.EXPORT	CRYPTO_memcmp,ENTRY,ARGW0=GR,ARGW1=GR,ARGW1=GR
+	.ALIGN	8
+CRYPTO_memcmp
+	.PROC
+	.CALLINFO	NO_CALLS
+	.ENTRY
+	cmpib,*=	0,$len,L\$no_data
+	xor		$rv,$rv,$rv
+
+L\$oop_cmp
+	ldb		0($in1),%r19
+	ldb		0($in2),%r20
+	ldo		1($in1),$in1
+	ldo		1($in2),$in2
+	xor		%r19,%r20,%r29
+	addib,*&lt;&gt;	-1,$len,L\$oop_cmp
+	or		%r29,$rv,$rv
+
+	sub		%r0,$rv,%r29
+	extru		%r29,31,1,$rv
+L\$no_data
+	bv		($rp)
+	.EXIT
+	nop
+	.PROCEND
+___
+}
+{
 my ($out,$cnt,$max)=(&quot;%r26&quot;,&quot;%r25&quot;,&quot;%r24&quot;);
 my ($tick,$lasttick)=(&quot;%r23&quot;,&quot;%r22&quot;);
 my ($diff,$lastdiff)=(&quot;%r21&quot;,&quot;%r20&quot;);
diff --git a/crypto/ppccpuid.pl b/crypto/ppccpuid.pl
index 8f603e9..9d1cada 100755
--- a/crypto/ppccpuid.pl
+++ b/crypto/ppccpuid.pl
@@ -177,6 +177,32 @@ Laligned:
 	.byte	0,12,0x14,0,0,0,2,0
 	.long	0
 .size	.OPENSSL_cleanse,.-.OPENSSL_cleanse
+
+globl	.CRYPTO_memcmp
+.align	4
+.CRYPTO_memcmp:
+	$CMPLI	r5,0
+	li	r0,0
+	beq	Lno_data
+	mtctr	r5
+Loop_cmp:
+	lbz	r6,0(r3)
+	addi	r3,r3,1
+	lbz	r7,0(r4)
+	addi	r4,r4,1
+	xor	r6,r6,r7
+	or	r0,r0,r6
+	bdnz	Loop_cmp
+
+Lno_data:
+	li	r3,0
+	sub	r3,r3,r0
+	extrwi	r3,r3,1,0
+	blr
+	.long	0
+	.byte	0,12,0x14,0,0,0,3,0
+	.long	0
+.size	.CRYPTO_memcmp,.-.CRYPTO_memcmp
 ___
 {
 my ($out,$cnt,$max)=(&quot;r3&quot;,&quot;r4&quot;,&quot;r5&quot;);
diff --git a/crypto/s390xcpuid.S b/crypto/s390xcpuid.S
index 3efad55..df7b35a 100644
--- a/crypto/s390xcpuid.S
+++ b/crypto/s390xcpuid.S
@@ -125,6 +125,33 @@ OPENSSL_cleanse:
 	br	%r14
 .size	OPENSSL_cleanse,.-OPENSSL_cleanse
 
+.globl	CRYPTO_memcmp
+.type	<A HREF="../../../mailman/listinfo/openssl-commits.html">CRYPTO_memcmp, at function</A>
+.align	16
+CRYPTO_memcmp:
+#if !defined(__s390x__) &amp;&amp; !defined(__s390x)
+	llgfr	%r4,%r4
+#endif
+	lghi	%r5,0
+	clgr	%r4,%r5
+	je	.Lno_data
+
+.Loop_cmp:
+	llc	%r0,0(%r2)
+	la	%r2,1(%r2)
+	llc	%r1,0(%r3)
+	la	%r3,1(%r3)
+	xr	%r1,%r0
+	or	%r5,%r1
+	brctg	%r4,.Loop_cmp
+
+	lnr	%r5,%r5
+	srl	%r5,31
+.Lno_data:
+	lgr	%r2,%r5
+	br	%r14
+.size	CRYPTO_memcmp,.-CRYPTO_memcmp
+
 .globl	OPENSSL_instrument_bus
 .type	<A HREF="../../../mailman/listinfo/openssl-commits.html">OPENSSL_instrument_bus, at function</A>
 .align	16
diff --git a/crypto/sparccpuid.S b/crypto/sparccpuid.S
index 6f1dded..f48d860 100644
--- a/crypto/sparccpuid.S
+++ b/crypto/sparccpuid.S
@@ -440,6 +440,40 @@ OPENSSL_cleanse:
 .type	OPENSSL_cleanse,#function
 .size	OPENSSL_cleanse,.-OPENSSL_cleanse
 
+.global	CRYPTO_memcmp
+.align	16
+CRYPTO_memcmp:
+	cmp	%o2,0
+#ifdef ABI64
+	beq,pn	%xcc,.Lno_data
+#else
+	beq	.Lno_data
+#endif
+	xor	%g1,%g1,%g1
+	nop
+
+.Loop_cmp:
+	ldub	[%o0],%o3
+	add	%o0,1,%o0
+	ldub	[%o1],%o4
+	add	%o1,1,%o1
+	subcc	%o2,1,%o2
+	xor	%o3,%o4,%o4
+#ifdef ABI64
+	bnz	%xcc,.Loop_cmp
+#else
+	bnz	.Loop_cmp
+#endif
+	or	%o4,%g1,%g1
+
+	sub	%g0,%g1,%g1
+	srl	%g1,31,%g1
+.Lno_data:
+	retl
+	mov	%g1,%o0
+.type	CRYPTO_memcmp,#function
+.size	CRYPTO_memcmp,.-CRYPTO_memcmp
+
 .global	_sparcv9_vis1_instrument_bus
 .align	8
 _sparcv9_vis1_instrument_bus:
diff --git a/crypto/x86_64cpuid.pl b/crypto/x86_64cpuid.pl
index 450550b..4946688 100644
--- a/crypto/x86_64cpuid.pl
+++ b/crypto/x86_64cpuid.pl
@@ -224,6 +224,28 @@ OPENSSL_cleanse:
 	jne	.Little
 	ret
 .size	OPENSSL_cleanse,.-OPENSSL_cleanse
+
+.globl  CRYPTO_memcmp
+.type   CRYPTO_memcmp,\@abi-omnipotent
+.align  16
+CRYPTO_memcmp:
+	xor	%rax,%rax
+	xor	%r10,%r10
+	cmp	\$0,$arg3
+	je	.Lno_data
+.Loop_cmp:
+	mov	($arg1),%r10b
+	lea	1($arg1),$arg1
+	xor	($arg2),%r10b
+	lea	1($arg2),$arg2
+	or	%r10b,%al
+	dec	$arg3
+	jnz	.Loop_cmp
+	neg	%rax
+	shr	\$63,%rax
+.Lno_data:
+	ret
+.size	CRYPTO_memcmp,.-CRYPTO_memcmp
 ___
 
 print&lt;&lt;___ if (!$win64);
diff --git a/crypto/x86cpuid.pl b/crypto/x86cpuid.pl
index 2b110ba..99ffa1d 100644
--- a/crypto/x86cpuid.pl
+++ b/crypto/x86cpuid.pl
@@ -365,6 +365,31 @@ for (@ARGV) { $sse2=1 if (/-DOPENSSL_IA32_SSE2/); }
 	&amp;ret	();
 &amp;function_end_B(&quot;OPENSSL_cleanse&quot;);
 
+&amp;function_begin_B(&quot;CRYPTO_memcmp&quot;);
+	&amp;push	(&quot;esi&quot;);
+	&amp;push	(&quot;edi&quot;);
+	&amp;mov	(&quot;esi&quot;,&amp;wparam(0));
+	&amp;mov	(&quot;edi&quot;,&amp;wparam(1));
+	&amp;mov	(&quot;ecx&quot;,&amp;wparam(2));
+	&amp;xor	(&quot;eax&quot;,&quot;eax&quot;);
+	&amp;xor	(&quot;edx&quot;,&quot;edx&quot;);
+	&amp;cmp	(&quot;ecx&quot;,0);
+	&amp;je	(&amp;label(&quot;no_data&quot;));
+&amp;set_label(&quot;loop&quot;);
+	&amp;mov	(&quot;dl&quot;,&amp;BP(0,&quot;esi&quot;));
+	&amp;lea	(&quot;esi&quot;,&amp;DWP(1,&quot;esi&quot;));
+	&amp;xor	(&quot;dl&quot;,&amp;BP(0,&quot;edi&quot;));
+	&amp;lea	(&quot;edi&quot;,&amp;DWP(1,&quot;edi&quot;));
+	&amp;or	(&quot;al&quot;,&quot;dl&quot;);
+	&amp;dec	(&quot;ecx&quot;);
+	&amp;jnz	(&amp;label(&quot;loop&quot;));
+	&amp;neg	(&quot;eax&quot;);
+	&amp;shr	(&quot;eax&quot;,31);
+&amp;set_label(&quot;no_data&quot;);
+	&amp;pop	(&quot;edi&quot;);
+	&amp;pop	(&quot;esi&quot;);
+	&amp;ret	();
+&amp;function_end_B(&quot;CRYPTO_memcmp&quot;);
 {
 my $lasttick = &quot;esi&quot;;
 my $lastdiff = &quot;ebx&quot;;
</PRE>


<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="007402.html">[openssl-commits] [openssl]  master update
</A></li>
	<LI>Next message: <A HREF="007405.html">[openssl-commits] [openssl]  master update
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#7404">[ date ]</a>
              <a href="thread.html#7404">[ thread ]</a>
              <a href="subject.html#7404">[ subject ]</a>
              <a href="author.html#7404">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="../../../mailman/listinfo/openssl-commits.html">More information about the openssl-commits
mailing list</a><br>
</body></html>
